%LET S_DEFAULT = 4390000;
%LET KNOWN_INFECTIONS = 150;
%LET KNOWN_CASES = 37;
/*Currently Hospitalized COVID-19 Patients*/
%LET CURRENT_HOSP = &KNOWN_CASES;
/*Doubling time before social distancing (days)*/
%LET DOUBLING_TIME = 5;
/*Social distancing (% reduction in social contact)*/
%LET RELATIVE_CONTACT_RATE = 0.00;
/**/
%LET ADMISSION_RATE=0.075;
/*factor to adjust %admission to make sense multiplied by Total I*/
%LET DIAGNOSED_RATE=1.0; 
/*Hospitalization %(total infections)*/
%LET HOSP_RATE = %SYSEVALF(&ADMISSION_RATE*&DIAGNOSED_RATE);
/*ICU %(total infections)*/
%LET ICU_RATE = %SYSEVALF(0.02*&DIAGNOSED_RATE);
/*Ventilated %(total infections)*/
%LET VENT_RATE = %SYSEVALF(0.01*&DIAGNOSED_RATE);
/*Hospital Length of Stay*/
%LET HOSP_LOS = 7;
/*ICU Length of Stay*/
%LET ICU_LOS = 9;
/*Vent Length of Stay*/
%LET VENT_LOS = 10;
/*default percent of total admissions that need ECMO*/
%LET ECMO_RATE=0.03; 
%LET ECMO_LOS=28;
/*default percent of admissions that need Dialysis*/
%LET DIAL_RATE=0.09;
%LET DIAL_LOS=10;
/*Hospital Market Share (%)*/
%LET MARKET_SHARE = 0.29;
%LET DEATH_RATE=0.00;
/*Regional Population*/
%LET S = &S_DEFAULT;
/*Currently Known Regional Infections (only used to compute detection rate - does not change projections*/
%LET INITIAL_INFECTIONS = &KNOWN_INFECTIONS;
%LET TOTAL_INFECTIONS = %SYSEVALF(&CURRENT_HOSP / &MARKET_SHARE / &HOSP_RATE);
%LET DETECTION_PROB = %SYSEVALF(&INITIAL_INFECTIONS / &TOTAL_INFECTIONS);
%LET I = %SYSEVALF(&INITIAL_INFECTIONS / &DETECTION_PROB);
%LET R = 0;
%LET INTRINSIC_GROWTH_RATE = %SYSEVALF(2 ** (1 / &DOUBLING_TIME) - 1);
%LET RECOVERY_DAYS = 14;
%LET GAMMA = %SYSEVALF(1/&RECOVERY_DAYS);
%LET BETA = %SYSEVALF((&INTRINSIC_GROWTH_RATE + &GAMMA) / &S * (1-&RELATIVE_CONTACT_RATE));
/*R_T is R_0 after distancing*/
%LET R_T = %SYSEVALF(&BETA / &GAMMA * &S);
%LET R_NAUGHT = %SYSEVALF(&R_T / (1-&RELATIVE_CONTACT_RATE));
/*doubling time after distancing*/
%LET DOUBLING_TIME_T = %SYSEVALF(1/%SYSFUNC(LOG2(&BETA*&S - &GAMMA + 1)));
%LET N_DAYS = 365;
%LET BETA_DECAY = 0.0;
/*Average number of days from infection to hospitalization*/
%LET DAYS_TO_HOSP = 0;

%PUT _ALL_;

/* DATA STEP APPROACH */
DATA DS_FINAL;
	FORMAT DATE ADMIT_DATE DATE9.;
	DO DAY = 0 TO &N_DAYS;
		IF DAY = 0 THEN DO;
			S_N = &S - (&I/&DIAGNOSED_RATE) - &R;
			I_N = &I/&DIAGNOSED_RATE;
			R_N = &R;
			BETA=&BETA;
			N = SUM(S_N, I_N, R_N);
		END;
		ELSE DO;
			BETA = LAG_BETA * (1- &BETA_DECAY);
			S_N = (-BETA * LAG_S * LAG_I) + LAG_S;
			I_N = (BETA * LAG_S * LAG_I - &GAMMA * LAG_I) + LAG_I;
			R_N = &GAMMA * LAG_I + LAG_R;
			N = SUM(S_N, I_N, R_N);
			SCALE = LAG_N / N;
			IF S_N < 0 THEN S_N = 0;
			IF I_N < 0 THEN I_N = 0;
			IF R_N < 0 THEN R_N = 0;
			S_N = SCALE*S_N;
			I_N = SCALE*I_N;
			R_N = SCALE*R_N;
		END;
		LAG_S = S_N;
		LAG_I = I_N;
		LAG_R = R_N;
		LAG_N = N;
		LAG_BETA = BETA;
		NEWINFECTED=ROUND(SUM(LAG(S_N),-1*S_N),1);
		IF NEWINFECTED < 0 THEN NEWINFECTED=0;
/*		HOSP = ROUND(I_N * &HOSP_RATE * &MARKET_SHARE);*/
/*		ICU = ROUND(I_N * &ICU_RATE * &MARKET_SHARE);*/
/*		VENT = ROUND(I_N * &VENT_RATE * &MARKET_SHARE);*/
/*		ECMO = ROUND(I_N * &ECMO_RATE * &MARKET_SHARE * &HOSP_RATE);*/
/*		DIAL = ROUND(I_N * &DIAL_RATE * &MARKET_SHARE * &HOSP_RATE);*/
		HOSP = ROUND(NEWINFECTED * &HOSP_RATE * &MARKET_SHARE);
		ICU = ROUND(NEWINFECTED * &ICU_RATE * &MARKET_SHARE);
		VENT = ROUND(NEWINFECTED * &VENT_RATE * &MARKET_SHARE);
		ECMO = ROUND(NEWINFECTED * &ECMO_RATE * &MARKET_SHARE * &HOSP_RATE);
		DIAL = ROUND(NEWINFECTED * &DIAL_RATE * &MARKET_SHARE * &HOSP_RATE);
		MARKET_HOSP = ROUND(NEWINFECTED * &HOSP_RATE);
		MARKET_ICU = ROUND(NEWINFECTED * &ICU_RATE);
		MARKET_VENT = ROUND(NEWINFECTED * &VENT_RATE);
		MARKET_ECMO = ROUND(NEWINFECTED * &ECMO_RATE * &HOSP_RATE);
		MARKET_DIAL = ROUND(NEWINFECTED * &DIAL_RATE * &HOSP_RATE);
		CUMULATIVE_SUM_HOSP + HOSP;
		CUMULATIVE_SUM_ICU + ICU;
		CUMULATIVE_SUM_VENT + VENT;
		CUMULATIVE_SUM_ECMO + ECMO;
		CUMULATIVE_SUM_DIAL + DIAL;
		CUMULATIVE_SUM_MARKET_HOSP + MARKET_HOSP;
		CUMULATIVE_SUM_MARKET_ICU + MARKET_ICU;
		CUMULATIVE_SUM_MARKET_VENT + MARKET_VENT;
		CUMULATIVE_SUM_MARKET_ECMO + MARKET_ECMO;
		CUMULATIVE_SUM_MARKET_DIAL + MARKET_DIAL;
		CUMADMITLAGGED=ROUND(LAG&HOSP_LOS(CUMULATIVE_SUM_HOSP),1) ;
		CUMICULAGGED=ROUND(LAG&ICU_LOS(CUMULATIVE_SUM_ICU),1) ;
		CUMVENTLAGGED=ROUND(LAG&VENT_LOS(CUMULATIVE_SUM_VENT),1) ;
		CUMECMOLAGGED=ROUND(LAG&ECMO_LOS(CUMULATIVE_SUM_ECMO),1) ;
		CUMDIALLAGGED=ROUND(LAG&DIAL_LOS(CUMULATIVE_SUM_DIAL),1) ;
		CUMMARKETADMITLAG=ROUND(LAG&HOSP_LOS(CUMULATIVE_SUM_MARKET_HOSP));
		CUMMARKETICULAG=ROUND(LAG&ICU_LOS(CUMULATIVE_SUM_MARKET_ICU));
		CUMMARKETVENTLAG=ROUND(LAG&VENT_LOS(CUMULATIVE_SUM_MARKET_VENT));
		CUMMARKETECMOLAG=ROUND(LAG&ECMO_LOS(CUMULATIVE_SUM_MARKET_ECMO));
		CUMMARKETDIALLAG=ROUND(LAG&DIAL_LOS(CUMULATIVE_SUM_MARKET_DIAL));
		ARRAY FIXINGDOT _NUMERIC_;
		DO OVER FIXINGDOT;
			IF FIXINGDOT=. THEN FIXINGDOT=0;
		END;
		HOSPITAL_OCCUPANCY= ROUND(CUMULATIVE_SUM_HOSP-CUMADMITLAGGED,1);
		ICU_OCCUPANCY= ROUND(CUMULATIVE_SUM_ICU-CUMICULAGGED,1);
		VENT_OCCUPANCY= ROUND(CUMULATIVE_SUM_VENT-CUMVENTLAGGED,1);
		ECMO_OCCUPANCY= ROUND(CUMULATIVE_SUM_ECMO-CUMECMOLAGGED,1);
		DIAL_OCCUPANCY= ROUND(CUMULATIVE_SUM_DIAL-CUMDIALLAGGED,1);
		MARKET_HOSPITAL_OCCUPANCY= ROUND(CUMULATIVE_SUM_MARKET_HOSP-CUMMARKETADMITLAG,1);
		MARKET_ICU_OCCUPANCY= ROUND(CUMULATIVE_SUM_MARKET_ICU-CUMMARKETICULAG,1);
		MARKET_VENT_OCCUPANCY= ROUND(CUMULATIVE_SUM_MARKET_VENT-CUMMARKETVENTLAG,1);
		MARKET_ECMO_OCCUPANCY= ROUND(CUMULATIVE_SUM_MARKET_ECMO-CUMMARKETECMOLAG,1);
		MARKET_DIAL_OCCUPANCY= ROUND(CUMULATIVE_SUM_MARKET_DIAL-CUMMARKETDIALLAG,1);	
		DATE = "13MAR2020"D + DAY;
		ADMIT_DATE = SUM(DATE, &DAYS_TO_HOSP.);

		OUTPUT;
	END;
	DROP LAG: BETA CUM: ;
RUN;

PROC SGPLOT DATA=DS_FINAL;
	TITLE "New Admissions - DATA Step Approach";
	SERIES X=DAY Y=HOSP;
	SERIES X=DAY Y=ICU;
	SERIES X=DAY Y=VENT;
	SERIES X=DAY Y=ECMO;
	SERIES X=DAY Y=DIAL;
	XAXIS LABEL="Days from Today";
	YAXIS LABEL="Daily Admissions";
RUN;

TITLE;

/*CENSUS*/
DATA HOSP_DISCHARGE (KEEP=DATE HOSP_DISCHARGE);
	SET DS_FINAL (DROP=DATE);
	FORMAT DATE DATE9.;
	DATE = SUM(ADMIT_DATE, &HOSP_LOS.);
	RENAME HOSP = HOSP_DISCHARGE;
RUN;

DATA ICU_DISCHARGE (KEEP=DATE ICU_DISCHARGE);
	SET DS_FINAL (DROP=DATE);
	FORMAT DATE DATE9.;
	DATE = SUM(ADMIT_DATE, &ICU_LOS.);
	RENAME ICU = ICU_DISCHARGE;
RUN;

DATA VENT_DISCHARGE (KEEP=DATE VENT_DISCHARGE);
	SET DS_FINAL (DROP=DATE);
	FORMAT DATE DATE9.;
	DATE = SUM(ADMIT_DATE, &VENT_LOS.);
	RENAME VENT = VENT_DISCHARGE;
RUN;

DATA ECMO_DISCHARGE (KEEP=DATE ECMO_DISCHARGE);
	SET DS_FINAL (DROP=DATE);
	FORMAT DATE DATE9.;
	DATE = SUM(ADMIT_DATE, &ECMO_LOS.);
	RENAME ECMO = ECMO_DISCHARGE;
RUN;

DATA DIAL_DISCHARGE (KEEP=DATE DIAL_DISCHARGE);
	SET DS_FINAL (DROP=DATE);
	FORMAT DATE DATE9.;
	DATE = SUM(ADMIT_DATE, &DIAL_LOS.);
	RENAME DIAL = DIAL_DISCHARGE;
RUN;

DATA ADMITS (KEEP=DATE HOSP ICU VENT ECMO DIAL);
	SET DS_FINAL (DROP=DATE);
	RENAME ADMIT_DATE = DATE;
RUN;

DATA CENSUS/*(KEEP=DATE CUMULATIVE_HOSP CUMULATIVE_ICU CUMULATIVE_VENT CUMULATIVE_ECMO CUMULATIVE_DIAL)*/;
	RETAIN CUMULATIVE_HOSP CUMULATIVE_ICU CUMULATIVE_VENT CUMULATIVE_ECMO CUMULATIVE_DIAL;
	MERGE ADMITS HOSP_DISCHARGE ICU_DISCHARGE VENT_DISCHARGE ECMO_DISCHARGE DIAL_DISCHARGE; 
	BY DATE;
	IF MISSING(HOSP_DISCHARGE) THEN HOSP_DISCHARGE=0;
	IF MISSING(ICU_DISCHARGE) THEN ICU_DISCHARGE=0;
	IF MISSING(VENT_DISCHARGE) THEN VENT_DISCHARGE=0;
	IF MISSING(ECMO_DISCHARGE) THEN ECMO_DISCHARGE=0;
	IF MISSING(DIAL_DISCHARGE) THEN DIAL_DISCHARGE=0;
	CUMULATIVE_HOSP + HOSP - HOSP_DISCHARGE;
	CUMULATIVE_ICU + ICU - ICU_DISCHARGE;
	CUMULATIVE_VENT + VENT - VENT_DISCHARGE;
	CUMULATIVE_ECMO + ECMO - ECMO_DISCHARGE;
	CUMULATIVE_DIAL + DIAL - DIAL_DISCHARGE;
	IF _N_ LE SUM(&N_DAYS,1);
RUN;

PROC SGPLOT DATA=CENSUS;
	TITLE "Census of Admissions";
	SERIES X=DATE Y=CUMULATIVE_HOSP;
	SERIES X=DATE Y=CUMULATIVE_ICU;
	SERIES X=DATE Y=CUMULATIVE_VENT;
	SERIES X=DATE Y=CUMULATIVE_ECMO;
	SERIES X=DATE Y=CUMULATIVE_DIAL;
	XAXIS LABEL="Date";
	YAXIS LABEL="Current Patients";
RUN;

TITLE;

/*PROC MODEL APPROACH*/

DATA DINIT(Label="Initial Coditions of Simulation"); 
	S_N = &S;
	I_N = &I; 
	R_N = &R;
	R_NAUGHT=&R_NAUGHT;
	DO TIME = 0 TO &N_DAYS; 
		OUTPUT; 
	END; 
RUN;

*Solvig the ODEs with Proc Model;
PROC TMODEL DATA = DINIT;
/* PARAMETER SETTINGS */ 
	PARMS N &S. R_NAUGHT &R_NAUGHT. ; 
	GAMMA = &GAMMA.;    	         
	BETA = R_NAUGHT*GAMMA/N;
	/* DIFFERENTIAL EQUATIONS */ 
	DERT.S_N = -BETA*S_N*I_N; 				
	DERT.I_N = BETA*S_N*I_N-GAMMA*I_N;   
	DERT.R_N = GAMMA*I_N;           
/* SOLVE THE EQUATIONS */ 
	SOLVE S_N I_N R_N / OUT = TMODEL_FINAL; 
RUN;
QUIT;

DATA TMODEL_FINAL;
	SET TMODEL_FINAL;
	HOSP = I_N * &HOSP_RATE * &MARKET_SHARE;
	ICU = I_N * &ICU_RATE * &MARKET_SHARE;
	VENT = I_N * &VENT_RATE * &MARKET_SHARE;
RUN;

PROC SGPLOT DATA=TMODEL_FINAL;
	TITLE "New Admissions - PROC MODEL Approach";
	SERIES X=TIME Y=HOSP;
	SERIES X=TIME Y=ICU;
	SERIES X=TIME Y=VENT;
	XAXIS LABEL="Days from Today";
	YAXIS LABEL="Daily Admissions";
RUN;

/* Post Process DS_FINAL and TMODEL_FINAL to:
calculate cumulative values
*/
/*
IDEA: Merge DS_FINAL and TMODEL_FINAL into FINAL.  
user variable model= to specify DS or TMODEL
synchronize variable names I_N & I for instance
*/
/* 
Ideas for more features:
Include Mike B.'s calculations
*/

TITLE;

CAS;

CASLIB _ALL_ ASSIGN;

PROC CASUTIL;
	DROPTABLE INCASLIB="CASUSER" CASDATA="PROJECT_DS" QUIET;
	LOAD DATA=WORK.DS_FINAL CASOUT="PROJECT_DS" OUTCASLIB="CASUSER" PROMOTE;
QUIT;


PROC CASUTIL;
	DROPTABLE INCASLIB="CASUSER" CASDATA="PROJECT_MODEL" QUIET;
	LOAD DATA=WORK.TMODEL_FINAL CASOUT="PROJECT_MODEL" OUTCASLIB="CASUSER" PROMOTE;
QUIT;

CAS CASAUTO TERMINATE;
